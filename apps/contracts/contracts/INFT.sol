// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.28;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/utils/ReentrancyGuard.sol";

interface IOracle {
    function verifyProof(bytes calldata proof) external view returns (bool);
}

contract INFT is ERC721, Ownable, ReentrancyGuard {
    uint256 public constant USAGE_FEE = 0.01 ether;

    struct AgentMetadata {
        string name;
        string description;
        string domain;
        string creatorName;
        string previewOutput;
        string intelligenceReference;
        string storageReference;
        string metadataURI;
    }

    // State variables
    mapping(uint256 => AgentMetadata) private _agentMetadata;
    mapping(uint256 => bytes32) private _metadataHashes;
    mapping(uint256 => string) private _encryptedURIs;
    mapping(uint256 => mapping(address => bytes)) private _authorizations;
    
    address public oracle;
    uint256 private _nextTokenId = 1;
    
    // Events
    event MetadataUpdated(uint256 indexed tokenId, bytes32 newHash);
    event UsageAuthorized(uint256 indexed tokenId, address indexed executor);
    event UsagePurchased(
        uint256 indexed tokenId,
        address indexed buyer,
        uint256 price
    );
    
    constructor(
        string memory name,
        string memory symbol,
        address _oracle
    ) ERC721(name, symbol) Ownable(msg.sender) {
        oracle = _oracle;
    }
    
    function mint(
        address to,
        string calldata encryptedURI,
        bytes32 metadataHash,
        AgentMetadata calldata metadata
    ) external onlyOwner returns (uint256) {
        uint256 tokenId = _nextTokenId++;
        _safeMint(to, tokenId);

        _agentMetadata[tokenId] = metadata;
        _encryptedURIs[tokenId] = encryptedURI;
        _metadataHashes[tokenId] = metadataHash;

        return tokenId;
    }
    
    function transfer(
        address from,
        address to,
        uint256 tokenId,
        bytes calldata sealedKey,
        bytes calldata proof
    ) external nonReentrant {
        require(ownerOf(tokenId) == from, "Not owner");
        require(IOracle(oracle).verifyProof(proof), "Invalid proof");
        
        // Update metadata access for new owner
        _updateMetadataAccess(tokenId, to, sealedKey, proof);
        
        // Transfer token ownership
        _transfer(from, to, tokenId);
        
        emit MetadataUpdated(tokenId, keccak256(sealedKey));
    }
    
    function authorizeUsage(
        uint256 tokenId,
        address executor,
        bytes calldata permissions
    ) external {
        require(ownerOf(tokenId) == msg.sender, "Not owner");
        _setUsageAuthorization(tokenId, executor, permissions);
    }

    function purchaseUsage(
        uint256 tokenId,
        bytes calldata permissions
    ) external payable nonReentrant {
        require(msg.value == USAGE_FEE, "Incorrect usage fee");

        address payable seller = payable(ownerOf(tokenId));
        _setUsageAuthorization(tokenId, msg.sender, permissions);

        (bool sent, ) = seller.call{value: msg.value}("");
        require(sent, "Payment failed");

        emit UsagePurchased(tokenId, msg.sender, msg.value);
    }
    
    function _updateMetadataAccess(
        uint256 tokenId,
        address newOwner,
        bytes calldata sealedKey,
        bytes calldata proof
    ) internal {
        // Extract new metadata hash from proof
        bytes32 newHash = bytes32(proof[0:32]);
        _metadataHashes[tokenId] = newHash;
        
        // Update encrypted URI if provided in proof
        if (proof.length > 64) {
            string memory newURI = string(proof[64:]);
            _encryptedURIs[tokenId] = newURI;
        }
    }
    
    function getMetadataHash(uint256 tokenId) external view returns (bytes32) {
        return _metadataHashes[tokenId];
    }
    
    function getEncryptedURI(uint256 tokenId) external view returns (string memory) {
        return _encryptedURIs[tokenId];
    }

    function getAgentMetadata(
        uint256 tokenId
    )
        external
        view
        returns (
            string memory name,
            string memory description,
            string memory domain,
            string memory creatorName,
            string memory previewOutput,
            string memory intelligenceReference,
            string memory storageReference,
            string memory metadataURI
        )
    {
        AgentMetadata storage metadata = _agentMetadata[tokenId];
        return (
            metadata.name,
            metadata.description,
            metadata.domain,
            metadata.creatorName,
            metadata.previewOutput,
            metadata.intelligenceReference,
            metadata.storageReference,
            metadata.metadataURI
        );
    }

    function getAuthorization(
        uint256 tokenId,
        address executor
    ) external view returns (bytes memory) {
        return _authorizations[tokenId][executor];
    }

    function _setUsageAuthorization(
        uint256 tokenId,
        address executor,
        bytes calldata permissions
    ) internal {
        _authorizations[tokenId][executor] = permissions;
        emit UsageAuthorized(tokenId, executor);
    }

    function tokenURI(uint256 tokenId) public view override returns (string memory) {
        require(_ownerOf(tokenId) != address(0), "Nonexistent token");

        string memory metadataURI = _agentMetadata[tokenId].metadataURI;
        return bytes(metadataURI).length > 0 ? metadataURI : "";
    }
}
